# Maintainer: Jacek Swierk <jacek.swierk@gmail.com>

pkgname=emacs-lsp-booster-git
pkgver=23.7ef6849
pkgrel=1
pkgdesc="Improves performance of lsp-mode using a wrapper executable."
arch=("x86_64")
url="https://github.com/blahgeek/emacs-lsp-booster"
license=("UNLICENSE")
depends=('gcc-libs')
makedepends=("git" "rust")
source=("${pkgname%-git}::git+${url}")
sha256sums=("SKIP")

pkgver() {
  cd "${srcdir}/${pkgname%-git}"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname%-git}"
  cargo build --release --locked
}

package() {
  cd "${srcdir}/${pkgname%-git}"
  install -Dm755 "target/release/emacs-lsp-booster" "${pkgdir}/usr/bin/emacs-lsp-booster"
}
